//===== rAthena Script ======================================= 
//= Renewal Acolyte Job Change
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Acolyte Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added back the npcs used for the priest quest.
//============================================================ 
prt_church,184,41,4	script	Cleric#aco	60,{
	if (Upper == 1) {
		if (ADVJOB == Job_High_Priest || ADVJOB == Job_Champion) {
			if (Class == Job_Novice_High) {
				mes "[Père Mareusis]";
				mes "Ah, je sens que vous avez enduré";
				mes "une vie pleine d'expériences. Vous avez dû apprendre bien des choses avant d'accéder au Valhalla";
				next;
				if (getskilllv("NV_BASIC") < 9) {
					mes "[Père Mareusis]";
					mes "Malheureusement, je ne pense pas que vous soyez prêt pour devenir Acolyte. Vous devez apprendre toutes les compétences de bases d'abord.";
					next;
					mes "[Père Mareusis]";
					mes "J'attendrai que vous soyez prêt.";
					mes "Que Dieu soit avec vous. ";
					close;
				}
				mes "[Père Mareusis]";
				mes "Bien, je vous souhaite la bienvenue";
				mes "au Valhalla et";
				mes "vous souhaite bonne chance";
				mes "dans votre nouveau périple.";
				next;
				skill 143,0,0;
				jobchange Job_Acolyte_High;
				skill 156,1,0;
				mes "[Père Mareusis]";
				mes "Maintenant, allez de l'avant et partez à la recherche de ceux qui ont besoin de votre aide. Que Dieu éclaire votre chemin.";
				close;
			}else{
				mes "[Père Mareusis]";
				mes "Maintenant, allez de l'avant et partez à la recherche de ceux qui ont besoin de votre aide. Que Dieu éclaire votre chemin.";
				close;
			}
		}else{
			mes "[Père Mareusis]";
			mes "Je sens que vous avez enduré une vie pleine d'expériences. Vous avez dû apprendre bien des choses avant d'accéder au Valhalla.";
			next;
			mes "[Père Mareusis]";
			mes "Cependant, je peux vous assurer que vous n'êtes pas fait pour être Acolyte. Rappelez vous de qui vous étiez et trouvez votre chemin.";
			close;
		}
	}
	mes "[Père Mareusis]";
	mes "Que recherchez-vous ?";
	next;
	switch(select("Passer Acolyte.:Que faut-il pour être Acolyte ?:S'en aller.")) {
	case 1:
		if(Class != Job_Novice) {
			mes "[Père Mareusis]";
			if(Class == Job_Acolyte) {
				mes "Vous sentez-vous bien aujourd'hui ? Je constate par votre tenue que vous êtes déjà un Acolyte. Vous ne vous moquez pas de moi, n'est-ce pas ?";
			}else{
				mes "Désolé, mais il me semble que vous avez déjà un métier, n'est-ce pas ?";
			}
			close;
		}else{
			mes "[Père Mareusis]";
			mes "Voulez-vous vraiment devenir un serviteur de Dieu ?";
			mes "Voyons voir si vous êtes prêt pour ça ou pas... Hm...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Père Mareusis]";
				mes "Oh mais?! Vous n'avez pas encore acquis les compétences de base ?! Vous avez un long chemin à faire ! Revenez quand vous aurez acquis les compétences !";
				close;
			}
			mes "[Père Mareusis]";
			mes "Hmm... votre level job est suffisant...";
			mes "Bien. Je vais vous donner les qualifications pour devenir Acolyte.";
			next;
			mes "[Père Mareusis]";
			mes "Rappelez vous de toujours apporter votre gratitude à Dieu, qui prend toujours soin de nous. Dans le chaos, et les temps durs, faites faces aux difficultés avec une foi inébranlable.";
			jobchange Job_Acolyte;
			getitem 1545,1;	// N_Mace
			next;
			mes "[Père Mareusis]";
			mes "Enfin, je vous félicite sincèrement d'avoir persévéré dans votre période d'essai.";
			close;
		}
	case 2:
		mes "[Père Mareusis]";
		mes "Voulez-vous devenir Acolyte ?";
		mes "Alors, vous devez remplir certaines exigences thinking those are the practices given by God."; //todo
		next;
		mes "[Père Mareusis]";
		mes "D'abord, vous devez atteindre au moins, en tant que débutant, le job level 9 ainsi qu'apprendre toutes les compétences de base.";
		mes "C'est la plus simple des choses à faire, alors considérez ça comme une façon de vous entraîner.";
		next;
		mes "[Père Mareusis]";
		mes "Quand vous estimerez avoir satisfait ces exigences, revenez me voir. Then you will have a holy job in which you can spread God's will."; //todo
		close;
	case 3:
		close;
	}
}

prt_fild03,365,255,2	script	Ascetic#aco	89,{
	mes "[Père Rubalkabara]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 6) {
			mes "Faites attention. Ils pourraient savoir que vous m'avez rencontré lorsque vous êtes venu au Sanctuaire de Prontera.";
			next;
			mes "[Père Rubalkabara]";
			mes "J'ai envoyé un pigeon transportant un message. J'espère qu'il y arrivera sans encombres...";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 2) {
				mes "Oh...? Vous devez être celui qui souhaite devenir Acolyte. J'ai déjà reçu les nouvelles du Sanctuaire qui disaient que vous pourriez arriver.";
				next;
				mes "[Père Rubalkabara]";
				mes "Maintenant, votre nom est " + strcharinfo(0) + ", c'est ça? Parfait, merci de m'avoir visité.";
				next;
				mes "[Père Rubalkabara]";
				mes "Je suppose que vous en avez entendu beaucoup à propos des Acolytes par Frère Mareusis. De plus, beaucoup de citoyens apportent leur aide au Sanctuaire de Prontera.";
				next;
				mes "[Father Rubalkabara]";
				mes "I guess there's really no need for me to teach you much. Besides, I'm sure your someone from your generation may have trouble listening to an old man like me. Hahaha~";
				next;
				mes "[Father Rubalkabara]";
				mes "Still, lessons may come from the places you'd least expect. God loves to teach his children in strange ways. You'll see.";
				next;
				mes "[Father Rubalkabara]";
				mes "Well, I'll send the message telling them that you've come to visit me. So, you may now return to the Prontera Sanctuary.";
				next;
				mes "[Father Rubalkabara]";
				mes "Farewell.";
				close2;
				savepoint "prt_fild03",361,255;
				set job_acolyte_q,6;
				end;
			}
			else {
				mes "Oh...";
				mes "Are you one of the";
				mes "Acolyte applicants...?";
				mes "Let's see...";
				next;
				mes "[Father Rubalkabara]";
				mes "Your name is " + strcharinfo(0) + "?";
				mes "I don't think your name";
				mes "is on my list. Hmmm...";
				next;
				mes "[Father Rubalkabara]";
				mes "Why don't you go back to the Prontera Sanctuary and check again?";
				close;
			}
		}
		else {
			mes "Huh? What brings you here? This is a very dangerous place for a Novice like yourself!";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherRub";
	else {
		if (BaseJob == Job_Priest) {
			mes "Greetings.";
			next;
			mes "[Father Rubalkabara]";
			mes "Welcome to the Deep. Feel free to sit and contemplate God's message with me. This place is beautiful, even if danger accompanies its sense of serenity...";
			close;
		}
		else {
			mes "Oh ho...";
			mes "Have you come into the Deep here for training? Or are you just a Wanderer?";
			next;
			mes "[Father Rubalkabara]";
			mes "Whoever you are, please take care of yourself. The monsters in here are shockingly strong, contrary to their cute appearance.";
			close;
		}
	}
}

moc_fild07,41,355,4	script	Ascetic#2aco	95,{
	mes "[Mother Mathilda]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 7) {
			mes "I will send a carrier pigeon to the Prontera Sanctuary. When you return, the Priest there should already have received my message.";
			next;
			mes "[Mother Mathilda]";
			mes "I will pray to God, and hope that you become an Acolyte soon.";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 3) {
				mes "Ah, you must be one of the Acolyte applicants. I sincerely welcome you.";
				next;
				mes "[Mother Mathilda]";
				mes "What is your name? " + strcharinfo(0) + "? Let's see... Ah, you're on my list.";
				next;
				mes "[Mother Mathilda]";
				mes "I will send a message to the Sanctuary confirming that you, " + strcharinfo(0) + " visited me and completed your penance.";
				next;
				mes "[Mother Mathilda]";
				mes "Please return to the Prontera Sanctuary and speak to the Priest in charge.";
				close2;
				savepoint "moc_fild07",35,355;
				set job_acolyte_q,7;
				end;
			}
			else {
				mes "Ah...!";
				mes "You must be one";
				mes "of the Acolyte applicants.";
				mes "I sincerely welcome you.";
				next;
				mes "[Mother Mathilda]";
				mes "Now, what is your name?";
				mes "" + strcharinfo(0) + "? Let's see...";
				next;
				mes "[Mother Mathilda]";
				mes "Hmm...";
				mes "It seems your name";
				mes "is not on my list...";
				next;
				mes "[Mother Mathilda]";
				mes "Perhaps you should return to the Prontera Sanctuary and check the destination for your penance trial once again.";
				close;
			}
		}
		else {
			mes "...";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_MotherMart";
	else {
		if (BaseJob == Job_Priest) {
			mes "Hello there~";
			next;
			mes "[Mother Mathilda]";
			mes "How is your practice coming along? I certainly hope you're enjoying living in the grace of God.";
			close;
		}
		else {
			mes "May God";
			mes "be with you...";
			close;
		}
	}
}

prt_fild00,208,218,6	script	Ascetic#3aco	98,{
	mes "[Father Yosuke]";
	if (BaseJob == Job_Novice) {
		if (job_acolyte_q == 8) {
			mes "What?";
			next;
			mes "[Father Yosuke]";
			mes "Have you any more business with me?! You don't! Go back to the Sanctuary now!";
			close;
		}
		if (job_acolyte_q != 0) {
			if (job_acolyte_q == 4) {
				mes "Hey.";
				mes "Whatever you are,";
				mes "you look like an";
				mes "Acolyte applicant.";
				mes "Right?";
				next;
				mes "[Father Yosuke]";
				mes "Not bad, not bad. You withstood the penance trial pretty well.";
				mes "So what's your name?";
				next;
				mes "[Father Yosuke]";
				mes "" + strcharinfo(0) + ", huh?";
				next;
				mes "[Father Yosuke]";
				mes "Okay. I'll send a message to the Sanctuary that you, " + strcharinfo(0) + ", came to visit me.";
				next;
				mes "[Father Yosuke]";
				mes "Now go back to the Santuary and finish becoming an Acolyte, kid.";
				close2;
				savepoint "prt_fild00",206,230;
				set job_acolyte_q,8;
				end;
			}
			else {
				mes "Hey.";
				mes "You look like an Acolyte Applicant. Am I right?";
				next;
				mes "[Father Yosuke]";
				mes "Not bad at all, you've made it all the way here from Prontera. So what's your name, kid?";
				next;
				mes "[Father Yosuke]";
				mes "" + strcharinfo(0) + ", huh? Why isn't your name on my list?";
				next;
				mes "[Father Yosuke]";
				mes "You probably made a mistake. Go back to the Santuary, and check with the Bishop.";
				close;
			}
		}
		else {
			mes "You...";
			mes "Novice.";
			mes "There something";
			mes "you wanna tell me?";
			close;
		}
	}
	else if (BaseJob == Job_Acolyte) callfunc "F_FatherYos";
	else {
		if (BaseJob == Job_Priest) {
			mes "Hey...";
			next;
			mes "[Father Yosuke]";
			mes "If you like, come sit here with me and meditate the great truths. God's majesty is truly inspiring...";
			close;
		}
		else {
			mes "Do you have anything to say? Because unfortunately for you,";
			mes "I don't any replies.";
			close;
		}
	}
}


//===== rAthena Script ======================================= 
//= Renewal Merchant Job Quest
//===== By: ================================================== 
//= Kisuka
//===== Current Version: ===================================== 
//= 1.1
//===== Compatible With: ===================================== 
//= rAthena SVN
//===== Description: ========================================= 
//= Job Change to Merchant Class
//===== Additional Comments: ================================= 
//= 1.0 First Version. [Kisuka]
//= 1.1 Added support for Baby classes. [Euphy]
//===== Translated in French by: =============================
//= Emerick42
//============================================================

alberta_in,53,43,6	script	Merchant#mer	86,{
	if (Upper == 1) {
		if (Class == Job_Novice_High && (ADVJOB == Job_Whitesmith || ADVJOB == Job_Creator)) {
			mes "[Chef Mahnsoo]";
			mes "Hey, ça fait longtemps qu'on ne s'est pas vu !";
			mes "Tu n'as pas laissé ton activité n'est-ce pas ?";
			mes "Que s'est-il passé ?";
			next;
			mes "[Chef Mahnsoo]";
			mes "Woah...";
			mes "Tu as carrément été au Valhalla ?! Ça fait loin...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Chef Mahnsoo]";
				mes "Hmmm...";
				mes "Tu n'as pas l'air de pouvoir redevenir Merchant pour l'instant. Va finir ton entraînement de Novice d'abord.";
				next;
				mes "[Chef Mahnsoo]";
				mes "Et ne t'inquiète pas, nous aurons toujours une place de Merchant pour toi. Reviens juste quand tu seras prêt, ok ?";
				close;
			}
			mes "[Chef Mahnsoo]";
			mes "Je suppose que c'est la destinée qui veut que nous nous rencontrions encore. Bien. Encore une fois, je vais te changer en Merchant !";
			next;
			skill 143,0,0;
			jobchange Job_Merchant_High;
			skill 153,1,0;
			skill 154,1,0;
			skill 155,1,0;
			mes "[Chef Mahnsoo]";
			mes "Ah, quelle nostalgie. Comme aux bons vieux temps ! Allez, va, et fait de ton mieux !";
			close;
		}else{
			mes "[Chef Mahnsoo]";
			mes "^333333*Baille*^000000";
			mes "Je m'ennuie tellement...";
			mes "Quand reverrais-je ma chère Blossom ?";
			close;
		}
	}
	mes "[Guildien Mahnsoo]";
	mes "Bien le bonjour, pourquoi êtes-vous là ?";
	next;
	switch(select("Je souhaiterais devenir merchant.:En savoir plus sur les merchants.:Demander les prérequis pour merchant.:Rien.")) {
	case 1:
		mes "[Guildien Mahnsoo]";
		if(BaseJob != Job_Novice) {
			if(BaseJob == Job_Merchant) {
				mes ".....? Quoi ? Qu'est-ce que vous dites ?";
				mes "Vous êtes déjà un merchant. Oh mon...";
				mes "Ah ah... Vous êtes marrant !";
			}else{
				mes "Hein ? Vous essayez d'avoir deux classes en même temps ?";
				mes "Pourquoi ne vous investiriez vous pas plus dans votre classe actuelle ?";
				next;
				mes "[Guildien Mahnsoo]";
				mes "Nous avons une éthique ici, vous savez.";
			}
			close;
		}else{
			mes "Vous souhaitez être un merchant ? Hmmm...";
			next;
			if (getskilllv("NV_BASIC") < 9) {
				mes "[Guildien Mahnsoo]";
				mes "Mais si vous voulez devenir merchant, votre capacité de base doit atteindre le niveau 9, et vous devez avoir utilisé tous vos points de capacités.";
				next;
				mes "[Guildien Mahnsoo]";
				mes "Ne pensez-vous pas que nous devons apprendre quelques basiques, malgré que nous ne faisions que nous occuper d'argent ?";
				close;
			}
			mes "[Guildien Mahnsoo]";
			mes "Bien, je pense que vous êtes totalement prêt pour ça, étant donné que votre capacité de base est à son niveau maximum.";
			mes "Maintenant, je vais vous autoriser à devenir merchant.";
			next;
			mes "[Guildien Mahnsoo]";
			mes "Toutes mes félicitations pour être devenu merchant !";
			mes "Félicitations aussi pour être devenu membre de la guilde des merchants, et un de nos collègues. Nous comptons sur votre investissement dans la guilde dorénavant !";
			callfunc "Job_Change",Job_Merchant;
			getitem 1381,1;	// N_Battle_Axe
			next;
			mes "[Guildien Mahnsoo]";
			mes "Tout à fait, nous avons besoin de jeunes gens pleins de passion pour atteindre notre but. Nous voulons représenter 20% du commerce mondial. Vous comprenez ?";
			mes "Bien, je disais juste... Qu'il faut faire un maximum d'argent au final. Vous comprenez ça au moins ~";
			close;
		}
	case 2:
		mes "[Guildien Mahnsoo]";
		mes "Merchant ? Basiquement, les gens qui vendent des objets pour faire de l'argent sont des merchants.";
		mes "Pas très bons pour se battre, et pas de capacités spéciales pour attaquer ou se soigner... Mais un merchant peut acheter des objets à bas prix et les revendre pour se faire de l'argent.";
		next;
		mes "[Guildien Mahnsoo]";
		mes "En fait, les merchants ont une capacité ultime appelée Mammonite, qui frappe les ennemis avec leur argent... Et ils peuvent équiper presque toutes les armes, sauf les Arcs, les Bâtons et les Épées à Deux Mains. Mais nous pouvons toujours les vendre.";
		mes "Et oui... Nous les merchants avons toujours le terme argent en tête. Vous comprenez ?";
		close;
	case 3:
		mes "[Guildien Mahnsoo]";
		mes "Pour devenir un merchant, et malgré que notre métier soit juste de vendre des objets et recevoir de l'argent, vous devez atteindre le niveau 9 pour votre capacité de base.";
		next;
		mes "[Guildien Mahnsoo]";
		mes "Sinon, nous avions l'habitude avant de faire payer une taxe d'entrée dans la guilde. Ce n'était pourtant pas si dur de gagner cet argent...?";
		mes "Mais il y a eu trop de gens qui n'arrivaient pas à rassembler la somme et continuaient de pleurer.";
		next;
		mes "[Guildien Mahnsoo]";
		mes "Alors récemment, nous avons décidé d'autoriser n'importe quel Novice qui le souhaitait à devenir un merchant.";
		mes "Après tout, ce sont leurs affaires s'ils arrivent à rester en vie ou non.";
		close;
	case 4:
		close;
	}
}
